plugins {
    id 'application'
}

dependencies {
    implementation project(':list')
    implementation project(':utilities')
    testImplementation 'junit:junit:4.12'
}

application {
    mainClassName = 'org.gradle.sample.Main'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    integrationTestImplementation 'junit:junit:4.12'
}

def integrationTest = tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter('test')
}

tasks.named('check') { dependsOn(integrationTest) }
